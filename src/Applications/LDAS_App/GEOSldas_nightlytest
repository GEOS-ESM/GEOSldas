#!/usr/bin/env csh

###################################################
#						  #
# USAGE: ./GEOSldas_nightlytest -e EMAIl -g TAG   #
#						  #
# This script runs Matt Thompson's LDAS_nighly	  #
#  test for user specified GEOSldas github tag.	  #
#						  #
# Arguments:					  #
#   -g Github tag name				  #
#   -e Users email address for summary   	  #
#             Sarith Mahanama and Weiyuan Jiang   #
#	                          (Mar 10, 2020)  #
###################################################

###################################################
# PROCESS COMMAND LINE AND GET EMAIL AND TAG NAME #
###################################################

if($#argv == 0) then
    echo "USAGE : ./GEOSldas_nightlytest -e EMAIl -g TAG "
    echo "   -g Github tag name"
    echo "   -e email address"
    echo "   -h this message"
    exit
endif

set arg_list=(`getopt -s tcsh -o hm:g:e: --long g-long,e-long: -- $argv:q`)

eval set argv=\($arg_list:q\)

while (1)
	switch($1:q)
	case -g:
	case --g-long:
		set GITTAG = $2 
                shift ; shift 
		breaksw
	case -e:
	case --e-long:
		set EMAIL = $2
                shift ; shift
		breaksw
	case -h:
                shift ; shift
		echo "-g Github tag name"
 		echo "-e email address"
                exit
 		breaksw
	case --:
		shift
		break
	default:
	        echo $2
		echo "Internal error!" ; exit 1
	endsw
end

if( $?GITTAG == 0 ) then
    echo "ERROR : GITHUB tag name is required " 
    ./GEOSldas_nightlytest
    exit
endif
if( $?EMAIL == 0 ) then
    echo "ERROR : Email address is required"
    ./GEOSldas_nightlytest
    exit
endif

###################################################
#       SET ENV VARIABLES AND CREATE DIRS         #
###################################################

setenv SYSTESTDIR /discover/nobackup/smahanam/GEOSldas_nightly/MTsystest/
#setenv SYSTESTDIR /gpfsm/dhome/mathomp4/SI_Team/scripts/systest/
setenv CURRENTTAG $GITTAG
setenv MYEMAIL $EMAIL

###################################################
#          COPY INPUT FILES AND UPDATE            # 
###################################################

mkdir -p /discover/nobackup/$USER/GEOSldas_nightly/scripts/input/LDAS/
/bin/cp -p $SYSTESTDIR/* /discover/nobackup/$USER/GEOSldas_nightly/scripts/.

set GROUPS    = `groups`
set GROUProot = $GROUPS[1]

# Update physics parameters based on the CURRENT tag.
#####################################################

source g5_modules;
./write_nightlytest_execfiles $SYSTESTDIR/input/LDAS/conus.model /discover/nobackup/$USER/GEOSldas_nightly/scripts/input/LDAS/conus.model $GROUProot 
./write_nightlytest_execfiles $SYSTESTDIR/input/LDAS/global.assim /discover/nobackup/$USER/GEOSldas_nightly/scripts/input/LDAS/global.assim $GROUProot 
./write_nightlytest_execfiles $SYSTESTDIR/input/LDAS/globalcscn.model /discover/nobackup/$USER/GEOSldas_nightly/scripts/input/LDAS/globalcscn.model $GROUProot 
./write_nightlytest_execfiles $SYSTESTDIR/input/LDAS/globalcs.model /discover/nobackup/$USER/GEOSldas_nightly/scripts/input/LDAS/globalcs.model $GROUProot 
./write_nightlytest_execfiles $SYSTESTDIR/input/LDAS/global.model /discover/nobackup/$USER/GEOSldas_nightly/scripts/input/LDAS/global.model $GROUProot 

###################################################
#                RUN Matt's SCRIPTS               # 
###################################################

cd /discover/nobackup/$USER/GEOSldas_nightly/scripts
/bin/ln -s /discover/swdev/gmao_SIteam/Baselibs/latest-mpiuni/Linux/bin/cdo
/bin/ln -s /discover/swdev/gmao_SIteam/Baselibs/latest-mpiuni/Linux/bin/nccmp

nohup ./LDAS_Nightly_Cron.csh &







